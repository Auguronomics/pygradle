/*
 * Copyright 2016 LinkedIn Corp.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'commons-io:commons-io:2.4'
    compile 'commons-lang:commons-lang:2.6'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'cglib:cglib:3.1'
    testCompile 'org.objenesis:objenesis:2.1'
    testCompile 'org.ow2.asm:asm:5.0.3'
    testCompile 'com.netflix.nebula:nebula-test:3.0.0'
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

task integTest(type: Test, dependsOn: assemble) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    description = 'Running integration tests'
    group = JavaBasePlugin.VERIFICATION_GROUP
    doFirst {
        def files = []
        files.addAll(configurations.getByName('archives').artifacts.files.collect { it.absolutePath })
        files.addAll(configurations.getByName('compile').files.collect { it.absolutePath })
        environment('test.dependencies', files.findAll{ !it.contains('wrapper/dists')}.join(","))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
